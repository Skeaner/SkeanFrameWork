import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-allopen"
apply plugin: "kotlin-kapt"


greendao {
    schemaVersion 1
}

allOpen {
    annotation("org.androidannotations.api.KotlinOpen")
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        //fixme 修改签名文件
        config {
            keyAlias 'test'
            keyPassword '123456'
            storeFile file('test.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        //fixme 修改程序ID
        applicationId "skean.yzsm.com.framework"

        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"


        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a"//, "x86"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex        : android.defaultConfig.applicationId + ".AppEventBusIndex",
                             "resourcePackageName": android.defaultConfig.applicationId]
            }
        }
        applicationVariants.all { variant ->
            variant.outputs.all {
                def dateStr = new SimpleDateFormat("MMddHHmm", Locale.US).format(new Date())
                def shortAppId = "${defaultConfig.applicationId}".substring("${defaultConfig.applicationId}".lastIndexOf(".") + 1)
                def versionTag = variant.buildType.isDebuggable() ? "d" : "r"
                outputFileName = "${shortAppId}-${variant.versionName}${versionTag}(b${variant.versionCode})-${dateStr}.apk"
            }
        }
    }
    buildTypes {
        //fixme 修改一些构建的设置
        def appTag = "\"" + "${defaultConfig.applicationId}".substring("${defaultConfig.applicationId}".lastIndexOf(".") + 1) + "\""
        def buglyAppIdDebug = "ccced6668f"
        def buglyAppIdRelease = "ccced6668f"
        def useExternalDatabase = true
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.config
            buildConfigField "String", "BUGLY_APPID", "\"${buglyAppIdDebug}\""
            buildConfigField "String", "APP_TAG", "${appTag}"
            buildConfigField "boolean", "USE_EXTERNAL_DB", "${useExternalDatabase}"
        }
        debug {
            signingConfig signingConfigs.config
            buildConfigField "String", "BUGLY_APPID", "\"${buglyAppIdRelease}\""
            buildConfigField "String", "APP_TAG", "${appTag}"
            buildConfigField "boolean", "USE_EXTERNAL_DB", "${useExternalDatabase}"
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'build/generated/source/greendao'
            jniLibs.srcDirs = ['libs']
            res.srcDirs = [
                    'src/main/res/_base',
                    'src/main/res/'
            ]
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/androidx.exifinterface_exifinterface.version'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://dl.bintray.com/skean/maven" }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.permissionsdispatcher:permissionsdispatcher:4.5.0"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:4.5.0"
    implementation 'org.androidannotations:androidannotations-api:4.6.0'
    kapt "org.androidannotations:androidannotations:4.6.0"
    kapt 'com.github.AleksanderMielczarek:AndroidAnnotationsPermissionsDispatcherPlugin:2.1.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    kapt 'org.greenrobot:eventbus-annotation-processor:3.1.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //谷歌库
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    //retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-jackson:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.2'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    //RxJava的库
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.13'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.2.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.2'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.2'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'
    //GreenDAO
    implementation 'org.greenrobot:greendao:3.2.2' //
    // sql-cipher支持
    implementation 'net.zetetic:android-database-sqlcipher:3.5.7@aar'
    //json解析器
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'
    //Apache的常用库
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'commons-net:commons-net:3.5'
    implementation 'org.apache.commons:commons-collections4:4.2'
    //软引用的handler
    implementation 'com.badoo.mobile:android-weak-handler:1.1'
    //图片加载Glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'
    //图片显示PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    //图片触摸库
    implementation 'it.sephiroth.android.library.imagezoom:imagezoom:2.3.0'
    //图片显示subsamplingImageView
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    //图片选择器matisse
    implementation('com.zhihu.android:matisse:0.5.2-beta4') {
        exclude module: "library"
    }
    //ViewPager指示器viewpagerindicator
    implementation "fr.avianey.com.viewpagerindicator:library:2.4.1.1@aar"
    //综合工具库utilcode
    implementation 'com.blankj:utilcode:1.25.9'
    //带进度显示的Webview
    implementation 'com.daimajia.numberprogressbar:library:1.2@aar'
    //带数字显示的numberprogressbar
    implementation 'com.tamic:browse:1.0.0'
    //下拉刷新ultra-ptr
    implementation 'in.srain.cube:ultra-ptr:1.0.11'
    //RecyclerView综合适配器
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.45-androidx'
    //material design对话框
    implementation 'com.afollestad.material-dialogs:core:3.1.1'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.1'
    //material design日期加时间选择器
    implementation 'com.appeaser.sublimepickerlibrary:sublimepickerlibrary:2.1.2'
    //权限提示
    implementation 'me.skean:easypermissiondialog:1.4'
    //文字Drawable
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    //bugly
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.8'
    //butterknife
    implementation 'com.jakewharton:butterknife:10.2.0'
    kapt 'com.jakewharton:butterknife-compiler:10.2.0'
}
