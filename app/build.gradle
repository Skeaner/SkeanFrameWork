import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

//fixme 替换对应的蒲公英
def pgyerAppIdBeta = "e290bd2ec835e2a986f93fee344a1b40"
def pgyerApiKeyBeta = "521cf96c91d333da545c8176e2bbdad2"
def pgyerAppIdFormat = "e290bd2ec835e2a986f93fee344a1b40"
def pgyerApiKeyFormat = "521cf96c91d333da545c8176e2bbdad2"

android {
    signingConfigs {
        //fixme 修改签名文件
        config {
            keyAlias 'test'
            keyPassword '123456'
            storeFile file('test.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        //fixme 修改程序ID
        applicationId "skean.yzsm.com.framework"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: android.defaultConfig.applicationId + ".AppEventBusIndex"]
            }
        }
        applicationVariants.all { variant ->
            variant.outputs.all {
                def dateStr = new SimpleDateFormat("yyyyMMddHHmm", Locale.US).format(new Date())
                def shortAppId = "${defaultConfig.applicationId}".substring("${defaultConfig.applicationId}".lastIndexOf(".") + 1)
                def versionTag = variant.buildType.isDebuggable() ? "d" : "r"
                outputFileName = "${shortAppId}-${variant.versionName}${versionTag}(b${variant.versionCode})-${dateStr}.apk"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-base.pro'
            shrinkResources true
            signingConfig signingConfigs.config
            buildConfigField "String", "PGYER_APPID", "\"${pgyerAppIdFormat}\""
            buildConfigField "String", "PGYER_APIKEY", "\"${pgyerApiKeyFormat}\""
            resValue "string", "PGYER_APPID", "${pgyerAppIdFormat}"
        }
        debug {
            signingConfig signingConfigs.config
            buildConfigField "String", "PGYER_APPID", "\"${pgyerAppIdBeta}\""
            buildConfigField "String", "PGYER_APIKEY", "\"${pgyerApiKeyBeta}\""
            resValue "string", "PGYER_APPID", "${pgyerAppIdBeta}"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            res.srcDirs = [
                    'src/main/res/_base',
                    'src/main/res/'
            ]
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    maven { url "https://raw.githubusercontent.com/Pgyer/mvn_repo_pgyer/master" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://dl.bintray.com/skean/maven" }
}

configurations.all {
    resolutionStrategy.eachDependency {  details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.1.0'
            }
        }
    }
}


dependencies {
    implementation('com.github.hotchemi:permissionsdispatcher:3.0.1') { exclude module: "support-v13" }
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.1.0'
    implementation  'org.androidannotations:androidannotations-api:4.4.0'
    annotationProcessor "org.androidannotations:androidannotations:4.4.0"
    annotationProcessor 'com.github.AleksanderMielczarek:AndroidAnnotationsPermissionsDispatcherPlugin:2.1.0'
    implementation  'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    implementation  fileTree(include: ['*.jar'], dir: 'libs')
    //谷歌库
    implementation  'com.android.support:appcompat-v7:26.1.0'
    implementation  'com.android.support:multidex:1.0.3'
    implementation  'com.android.support:recyclerview-v7:26.1.0'
    implementation  'com.android.support:design:26.1.0'
    //retrofit2
    implementation  'com.squareup.retrofit2:retrofit:2.3.0'
    implementation  'com.squareup.retrofit2:converter-jackson:2.3.0'
    implementation  'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation  'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation  'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    //RxJava的库
    implementation  'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation  'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation  'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation  'com.jakewharton.rxbinding2:rxbinding-support-v4:2.0.0'
    implementation  'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'
    implementation  'com.jakewharton.rxbinding2:rxbinding-design:2.0.0'
    implementation  'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.0.0'
    //DBFlow
    implementation  "com.github.Raizlabs.DBFlow:dbflow-core:4.2.3"
    implementation  "com.github.Raizlabs.DBFlow:dbflow:4.2.3"
    implementation  "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:4.2.3"
    // sql-cipher支持
    implementation  'net.zetetic:android-database-sqlcipher:3.5.7@aar'
    // sql-cipher支持
    implementation  "com.github.Raizlabs.DBFlow:dbflow-rx2:4.2.3"
    // RXJava支持

    //MVP框架
    implementation  'com.hannesdorfmann.mosby3:mvp:3.0.0'
    implementation  'com.hannesdorfmann.mosby3:viewstate:3.0.0'
    implementation  'com.hannesdorfmann.mosby3:mvi:3.0.0'
    //json解析器
    implementation  'com.fasterxml.jackson.core:jackson-core:2.9.4'
    implementation  'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    implementation  'com.fasterxml.jackson.core:jackson-annotations:2.9.4'
    //三方工具
    implementation  'commons-codec:commons-codec:1.10'
    //阿帕奇commons-codec
    implementation  'commons-net:commons-net:3.5'
    //阿帕奇commons-net
    implementation  'org.apache.commons:commons-collections4:4.1'
    //阿帕奇commons-collections4
    implementation  'com.badoo.mobile:android-weak-handler:1.1'
    //软引用的handler
    implementation  'com.squareup.picasso:picasso:2.5.2'
    //图片加载picasso
    implementation  'com.pgyersdk:sdk:2.7.9'
    //蒲公英sdk
    implementation  'com.github.chrisbanes:PhotoView:2.1.3'
    //图片显示PhotoView
    implementation  'it.sephiroth.android.library.imagezoom:imagezoom:2.3.0'
    //图片触摸库
    implementation  'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
    //图片显示subsamplingImageView
    implementation  'com.zhihu.android:matisse:0.5.0-mod@aar'
    //图片选择器matisse
    implementation  'fr.avianey.com.viewpagerindicator:library:2.4.1.1@aar'
    //ViewPager指示器viewpagerindicator
    implementation  'com.blankj:utilcode:1.9.8'
    //综合工具库utilcode
    implementation  'com.tamic:browse:1.0.0'
    //带进度显示的Webview
    implementation  'com.daimajia.numberprogressbar:library:1.2@aar'
    //带数字显示的numberprogressbar
    implementation  'in.srain.cube:ultra-ptr:1.0.11'
    //下拉刷新ultra-ptr
    implementation  'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    //RecyclerView综合适配器
    implementation  'com.afollestad.material-dialogs:core:0.9.6.0'
    //material design对话框
    implementation  'com.appeaser.sublimepickerlibrary:sublimepickerlibrary:2.1.1'
    //material design日期加时间选择器
    implementation  'me.skean:easypermissiondialog:1.3'
    //权限提示
    implementation  'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    //文字Drawable
}
